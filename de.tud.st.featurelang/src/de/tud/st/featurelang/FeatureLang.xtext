grammar de.tud.st.featurelang.FeatureLang with org.eclipse.xtext.common.Terminals

generate featureLang "http://www.tud.de/st/featurelang/FeatureLang"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

FeatureRequest:
	(statements+= Statement)*;
	
Statement:
	(ChangeStatement | CreationStatement) ('.')?
;
	
ChangeStatement hidden(WS):
	target=Class
	((priority=Priority (negation ?= 'not')? action=Action) | update=UpdateAction)
;	

CreationStatement hidden(WS):
	('it' | 'there') priority=Priority (negation ?= 'not')? ('exist' | 'be') classElement=Class
;

Action hidden(WS):
	type=(AttributeAction | AssociationAction | InheritanceAction)
;

UpdateAction hidden(WS):
	'has' 'an' ('existing')?
	attribute=Attribute '.' 'it' priority=Priority 'be' 'a' type=Datatype
;

AttributeAction hidden(WS):
	'have' ('the' | 'an') attribute=Attribute '.'
	('it' 'is' 'a' type=Datatype)?
;

AssociationAction hidden(WS):
	'have' 'associations' 'to' target=Class 'called' relation=ID
;
	
InheritanceAction hidden(WS):
	'inherit' 'from' parent=Class
;

Class hidden(WS):
	('the' | 'a')? ('class')? name=ID
;

Attribute hidden(WS):
	('attribute')? name=ID
;
	
Priority hidden(WS):
	priority=PriorityValue
;

enum PriorityValue:
	SHOULD='should' | MUST='must'
;

enum Datatype:
	WORD='time' | PHRASE='phrase' | NUMBER='number'
;